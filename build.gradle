import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'idea'
    id 'kr.entree.spigradle' version '2.2.3'
}

apply plugin: 'java'

group = 'com.vergilprime'
version = '0.4'
jar.archiveFileName = 'AngelInventories.jar'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.3-R0.1-SNAPSHOT'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.xerial:sqlite-jdbc:3.32.3.2'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

spigot {
    debug {
        buildVersion '1.16.4'
        jvmArgs '-Xmx4G'
        serverPort 25565
    }
    // The 'name' and 'version' will be set to project.version and project.name,
    // But we may set those manually.
    name = 'AngelInventories'
    apiVersion = "1.16"
    website = "Angels-Reach.com"
    version = project.version
    authors = ["VergilPrime"]
    commands {
        AngelInventories {
            aliases = ['ai']
            description = "Use /AI Save <name> to create custom inventories or /AI Set [player] <name> to replace a player's inventory with a custom one."
            permission = 'AngelInventories.Admin'
            usage = '/<command> <save|set> [player] [customInventoryName]'
        }
        ToggleInventory {
            aliases = ['ti']
            description = "Toggle through your available player inventories."
            usage = '/<command> [#]'
        }
        RestoreInventory {
            description = "Set a player with a Custom Inventory back to their last equipped player inventory"
            permission = 'AngelInventories.Admin'
            usage = "/<command> [playername]"
        }
    }
    permissions {
        "AngelInventories.Admin" {
            description = 'Allows AI Save and Set commands, and ResetInventory command.'
            defaults = 'op'
        }
    }
}
